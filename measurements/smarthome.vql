package hu.bme.mit.inf.dslreasoner.domains.smarthome.queries

import "http://www.example.org/smarthome"
import "http://www.eclipse.org/emf/2002/Ecore"
import java hu.bme.mit.inf.dslreasoner.domains.smarthome.utilities.SmarthomeHelper
import java hu.bme.mit.inf.querytransformation.query.KGate
//every time the temperature of the house has increased 2 or more degrees in less than one minute
pattern tempIncr(measurement:Measurement, time: UReal){
	find tempIncrBE(_, measurement,_);
	Measurement.time(measurement, time);
	find incrementbase(_);
}

@BasicEvent(probability=p)
pattern tempIncrBE(m1: Measurement, m2: Measurement, p: java Double){
	Home.measurements(home, m1);
	Home.measurements(home, m2);
	Measurement.time(m1,ti1);
	Measurement.time(m2,ti2);
	Measurement.temp(m1,te1);
	Measurement.temp(m2,te2);
	check(SmarthomeHelper.incrementable(te1,ti1,te2,ti2));
	p == eval(SmarthomeHelper.incrementConfidence(te1,ti1,te2,ti2));
}
@BasicEvent(probability=0.925)
pattern incrementbase(h:Smarthome){
	Smarthome(h);
}

//when four TempIncr events are detected in less than 5 minutes, while temperature remains above 33 degrees
pattern tempWarning(home: Home, m4: Measurement){
	find fourWarning(home,m1,m2,m3,m4);
	find tempIncr(m1,_);
	find tempIncr(m2,_);
	find tempIncr(m3,_);
	find tempIncr(m4,_);
	find warningbase(_);
}
@NoEvent
pattern fourWarning(home: Home, m1: Measurement, m2: Measurement, m3: Measurement, m4: Measurement){
	find incrementAfter3(m1,m2, m3);
	find incrementAfter2(m3,m4);
	Measurement.time(m1,t1);
	Measurement.time(m4,t4);
	Home.measurements(home, m4);
	check(SmarthomeHelper.within5m(t1,t4));
}
@NoEvent
pattern incrementAfter3(m1: Measurement, m2: Measurement, m3: Measurement){
//	Home.measurements(home, m1);
//	Home.measurements(home, m3);
	find incrementAfter2(m1,m2);
	find incrementAfter2(m2,m3);
//	Home.measurements(home,m2);
	Measurement.time(m1,t1);
//	Measurement.time(m2,t2);
	Measurement.time(m3,t3);
//	m1!=m3;
//	m2!=m3;
	check(SmarthomeHelper.within5m(t1,t3));
}

@NoEvent
pattern incrementAfter2(m1: Measurement, m2: Measurement){
	Home.measurements(home,m1);
	Home.measurements(home,m2);
	Measurement.time(m1,t1);
	Measurement.time(m2,t2);
	find tempIncrBE(_,m1,_);
	find tempIncrBE(_,m2,_);
	m1!=m2;
	check(SmarthomeHelper.after(t1,t2));
	check(SmarthomeHelper.within5m(t1,t2));
}
@BasicEvent(probability=0.925)
pattern warningbase(h:Smarthome){
	Smarthome(h);
}

//each time the CO levels exceed 5000 units
@BasicEvent(probability=p)
pattern coHigh(meas: Measurement, p: java Double){
	Measurement.co(meas, co);
	check(SmarthomeHelper.gt5000(co));
	p == eval(SmarthomeHelper.gt5000Confidence(co));
}

//a COHigh event is detected, followed by a TempWarning event within less than 5 seconds
pattern fireWarning(co: Measurement){
	Home.measurements(home,co);
	find coHigh(co, _);
	Measurement.time(co,cotime);
	find tempWarning(home,m4);
	Measurement.time(m4,twtime);
	check(SmarthomeHelper.within5s(cotime,twtime));
}

//the main door of the house is closed and there is no person is at home
pattern nobodyHome(m: Measurement){
	find atHome(m,p);
	neg find personBE(p,_);
} or {
	neg find atHome(m,_);
}

private pattern atHome(m:Measurement, p: Person){
	Measurement.athome(m,p);
}
@BasicEvent(probability=p)
pattern personBE(person: Person, p: EDouble){
	Person.confidence(person,p);
}
//if a FireWarning occurs after a NobodyHome object is created
pattern call(measurement: Measurement){
	find fireWarning(measurement);
	find nobodyHome(measurement);
}

@Weight(lhsname="p",	 
	class="hu.bme.mit.inf.dslreasoner.domains.smarthome.utilities.SmarthomeHelper",
	function="P"
)
pattern callProbability(measurement: Measurement, p:java Double){
	p == KGate find call(measurement);
}
/*
pattern stochasticCount(cnt: java Integer){
	cnt1 == count find call(_);
	cnt2 == count find nobodyHome(_);
	cnt3 == count find fireWarning(_);
	cnt4 == count find tempWarning(_,_);
	cnt5 == count find tempIncr(_,_);
	cnt == eval(cnt1+cnt2+cnt3+cnt4+cnt5);
}*/
